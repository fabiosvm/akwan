
chunk         ::= stmt* EOF

stmt          ::= "let" NAME ( ":" type )? ( "=" expr )? ";"
                | NAME "=" expr ";"
                | "return" expr? ";"
                | "{" stmt* "}"
                | expr ";"

type          ::= "Bool" | "Number"

expr          ::= add_expr ( ".." add_expr )?

add_expr      ::= mul_expr ( ( "+" | "-" ) mul_expr )*

mul_expr      ::= unary_expr ( ( "*" | "/" | "%" ) unary_expr )*

unary_expr    ::= "-" unary_expr | prim_expr

prim_expr     ::= "nil" | "false" | "true" | INT | NUMBER | STRING
                | "[" ( expr ( "," expr )* )? "]"
                | NAME ( "[" expr "]" )*
                | "(" expr ")"
