
chunk         ::= stmt* EOF

stmt          ::= "let" NAME ( "=" expr )? ";"
                | NAME subscr* "=" expr ";"
                | "return" expr? ";"
                | "{" stmt* "}"
                | expr ";"

subscr        ::= "[" expr "]"

expr          ::= add_expr ( ".." add_expr )?

add_expr      ::= mul_expr ( ( "+" | "-" ) mul_expr )*

mul_expr      ::= unary_expr ( ( "*" | "/" | "%" ) unary_expr )*

unary_expr    ::= "-" unary_expr | prim_expr

prim_expr     ::= "nil" | "false" | "true" | INT | NUMBER | STRING
                | "[" ( expr ( "," expr )* )? "]"
                | NAME subscr*
                | "(" expr ")"
