
chunk         ::= stmt* EOF

stmt          ::= "let" NAME ( "=" expr )? ";"
                | NAME "=" expr ";"
                | "return" expr? ";"
                | expr ";"

expr          ::= add_expr ( ".." add_expr )?

add_expr      ::= mul_expr ( ( "+" | "-" ) mul_expr )*

mul_expr      ::= unary_expr ( ( "*" | "/" | "%" ) unary_expr )*

unary_expr    ::= "-" unary_expr | call_expr

call_expr     ::= prim_expr ( "(" ( expr ( "," expr )* )? ")" )*

prim_expr     ::= "nil" | "false" | "true" | INT | NUMBER | STRING
                | "fn" "(" ( NAME ( "," NAME )* )? ")" "{" stmt* "}"
                | NAME
                | "(" expr ")"
