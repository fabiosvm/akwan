
chunk         ::= stmt* EOF

stmt          ::= "let" NAME ( "=" expr )? ";"
                | NAME ( "=" expr | call* ) ";"
                | "return" expr? ";"
                | "{" stmt* "}"
                | expr ";"

call          ::= "(" ( expr ( "," expr )* )? ")"

expr          ::= add_expr ( ".." add_expr )?

add_expr      ::= mul_expr ( ( "+" | "-" ) mul_expr )*

mul_expr      ::= unary_expr ( ( "*" | "/" | "%" ) unary_expr )*

unary_expr    ::= "-" unary_expr | call_expr

call_expr     ::= prim_expr call*

prim_expr     ::= "nil" | "false" | "true" | INT | NUMBER | STRING
                | "[" ( expr ( "," expr )* )? "]"
                | NAME ( "[" expr "]" )*
                | "(" expr ")"
